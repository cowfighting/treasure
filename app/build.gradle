apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.xycoding.treasure"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile file('key.jks')
            storePassword "xymelon"
            keyAlias "melon"
            keyPassword "xymelon"
        }
        release {
            storeFile file('key.jks')
            storePassword "xymelon"
            keyAlias "melon"
            keyPassword "xymelon"
        }
    }

    dataBinding {
        enabled = true
    }

    useLibrary 'org.apache.http.legacy'

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    flavorDimensions "vendor"

    productFlavors {
        index { dimension "vendor" }
        tencent { dimension "vendor" }
        union1 { dimension "vendor" }
    }

    project.afterEvaluate {
        //清空文件夹
        task delPackDir(type: Delete) {
            delete '../pack/release', '../pack/market', '../pack/union'
            followSymlinks = true
        }

        task encrypt(type: Exec) {
            workingDir '../pack'
            commandLine 'python', 'encrypt.py'
        }
        android.applicationVariants.all { variant ->
            //市场包文件夹
            def marketDir = rootProject.file("pack/market")
            //union包文件夹
            def unionDir = rootProject.file("pack/union")

            //是否union包
            def isUnion = variant.flavorName.contains("union")
            def apkFile
            if (isUnion) {
                apkFile = new File(unionDir, String.format("treasure-%s-release.apk", variant.flavorName))
            } else {
                apkFile = new File(marketDir, String.format("Treasure_Android_%s_%s.apk", variant.versionName, variant.flavorName))
            }

            variant.outputs.all { output ->
                Task copyAndRenameAPKTask = project.task("copyAndRename${variant.name.capitalize()}APK", type: Copy) {
                    from output.outputFile.getParent()
                    if (isUnion) {
                        into unionDir
                    } else {
                        into marketDir
                    }
                    include output.outputFileName
                    rename(output.outputFileName, apkFile.getName())
                }
                //生成release包前清空文件夹
                variant.assemble.dependsOn(delPackDir)

                copyAndRenameAPKTask.mustRunAfter(variant.assemble)
                variant.assemble.finalizedBy(copyAndRenameAPKTask)

                encrypt.mustRunAfter(copyAndRenameAPKTask)
                copyAndRenameAPKTask.finalizedBy(encrypt)
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofitVersion"
    //RxBinding begin
    implementation "com.jakewharton.rxbinding2:rxbinding:$rootProject.ext.rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$rootProject.ext.rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rootProject.ext.rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-design:$rootProject.ext.rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rootProject.ext.rxBindingVersion"
    //RxBinding end
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'com.flaviofaria:kenburnsview:1.0.7'
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    implementation 'com.aurelhubert:ahbottomnavigation:1.3.3'
    //调试 begin
    implementation "com.facebook.stetho:stetho:$rootProject.ext.stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$rootProject.ext.stethoVersion"
    //调试 end

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.facebook.fbui.textlayoutbuilder:textlayoutbuilder:1.4.0'

    implementation 'com.github.xymelon:richtext:1.0.10'
}
