apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.xycoding.treasure"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile file('key.jks')
            storePassword "xymelon"
            keyAlias "melon"
            keyPassword "xymelon"
        }
        release {
            storeFile file('key.jks')
            storePassword "xymelon"
            keyAlias "melon"
            keyPassword "xymelon"
        }
    }

    dataBinding {
        enabled = true
    }

    useLibrary 'org.apache.http.legacy'

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    flavorDimensions "vendor"

    productFlavors {
        index { dimension "vendor" }
        tencent { dimension "vendor" }
        union1 { dimension "vendor" }
    }

//    android.applicationVariants.all { variant ->
//        variant.outputs.all {
//            if (variant.flavorName.contains("union")) {
//                outputFileName = "treasure_${variant.flavorName}_release.apk"
//            } else {
//                outputFileName = "Treasure_${variant.versionName}_${variant.flavorName}.apk"
//            }
//        }
//    }

    project.afterEvaluate {
        task encrypt(type: Exec) {
            workingDir '../pack'
            commandLine 'python', 'encrypt.py'
        }
        android.applicationVariants.all { variant ->
            //市场包文件夹
            def marketDir = rootProject.file("pack/market")
            //union包文件夹
            def unionDir = rootProject.file("pack/union")

            //是否union包
            def isUnion = variant.flavorName.contains("union")
            def apkFile
            if (isUnion) {
                apkFile = new File(unionDir, String.format("treasure-%s-release.apk", variant.flavorName))
            } else {
                apkFile = new File(marketDir, String.format("Treasure_Android_%s_%s.apk", variant.versionName, variant.flavorName))
            }

            variant.outputs.all { output ->
                Task copyAndRenameAPKTask = project.task("copyAndRename${variant.name.capitalize()}APK", type: Copy) {
                    from output.outputFile.getParent()
                    if (isUnion) {
                        into unionDir
                    } else {
                        into marketDir
                    }
                    include output.outputFileName
                    rename(output.outputFileName, apkFile.getName())
                }
                copyAndRenameAPKTask.mustRunAfter(variant.assemble)
                variant.assemble.finalizedBy(copyAndRenameAPKTask)

                encrypt.mustRunAfter(copyAndRenameAPKTask)
                copyAndRenameAPKTask.finalizedBy(encrypt)
            }
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.ext.supportLibraryVersion"

    compile 'io.reactivex.rxjava2:rxjava:2.1.6'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofitVersion"

    //RxBinding begin
    compile "com.jakewharton.rxbinding2:rxbinding:$rootProject.ext.rxBindingVersion"
    compile "com.jakewharton.rxbinding2:rxbinding-support-v4:$rootProject.ext.rxBindingVersion"
    compile "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rootProject.ext.rxBindingVersion"
    compile "com.jakewharton.rxbinding2:rxbinding-design:$rootProject.ext.rxBindingVersion"
    compile "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rootProject.ext.rxBindingVersion"
    //RxBinding end

    compile 'com.squareup.okhttp3:okhttp:3.9.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile 'com.flaviofaria:kenburnsview:1.0.7'
    compile 'com.github.chrisbanes.photoview:library:1.2.4'
    compile 'com.aurelhubert:ahbottomnavigation:1.3.3'

    //调试 begin
    compile "com.facebook.stetho:stetho:$rootProject.ext.stethoVersion"
    compile "com.facebook.stetho:stetho-okhttp3:$rootProject.ext.stethoVersion"
    //调试 end

}
